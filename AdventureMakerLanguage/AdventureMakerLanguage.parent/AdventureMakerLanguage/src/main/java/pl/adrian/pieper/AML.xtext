grammar pl.adrian.pieper.AML with org.eclipse.xtext.common.Terminals

generate aML "http://www.adrian.pl/pieper/AML"

Model:
    elements+=Element*;

Element:
    Location | CharacterType | Monster | Adventure | Item | Skill;

Location:
    'location' name = ID place = Place;

Place:
    GPSLoc | TagLoc;

GPSLoc:
    'in radius of' radius=INT 'meters from' '?'
;

TagLoc:
    'tagged as' tag = ID
;

Skill:
    ActiveSkill | PassiveSkill
;

ActiveSkill:
    'active' 'skill' name=ID '{'
        'effect' ':' effect=Effect
        'mp' ':' mpCost=INT
        ('cooldown' ':' cooldown=INT 'turns')?
    '}'
;

Effect:
    'take' hp=DamageFormula 'damage' ('for' times=INT 'turns')?
;

DamageFormula:
    value=Value (expression=Expression)?
;

enum OperationType:
    plus='+' |
    minus='-' |
    divide='/' |
    multiply='*'
;

Expression:
    operation=OperationType right=DamageFormula
;

Brackets:
    '(' formula=DamageFormula ')'
;

Value:
    Variable | Constant | Brackets
;

enum VariableType:
    INTELIGENCE='int' |
    STRONG='str' |
    AGILITY='agi'
;

Variable:
    value = VariableType
;

Constant:
    value = INT
;

PassiveSkill:
    'passive' 'skill' name=ID
;

CharacterType:
    'character' 'type' name=ID '{'
        'stats on start:'
            hp=INT 'hp'
            mp=INT 'mp'
            str=INT 'str'
            int=INT 'int'
        'stats per lvl:'
            '+' hpPerLevel=INT 'hp'
            '+' mpPerLevel=INT 'mp'
            '+' strPerLevel=INT 'str'
            '+' intPerLevel=INT 'int'
        'skills tree:'
        (skillsTrees+=SkillsNode)*
    '}';

SkillsNode:
    skill=[Skill] ('=>'
    '{' ( childs +=SkillsNode)* '}')?
;


Monster:
    'monster' name=ID '{'
    stats=Stats
    (loot=Loot)?
    '}';

Loot:
    'loot' ':' ('gold' goldMin=INT '-' goldMax=INT)? (items+=LootItem (',' items+=LootItem)*)
;

LootItem:
    type=[Item] chance=INT '%'
;

Stats:
    'power' ':' power=INT
    'hp' ':' hp=INT
    'exp' ':' exp=INT;

Adventure:
    'adventure in the' location=[Location] '{'
        startStage=Stage
    '}';

Stage:
    MakeDecision | Test | Fight ;

Test:
    Condition pos=Stage ('else' neg=Stage)?;

Condition:
    'if you\'re' (negation?='not')? contitionType=ConditionType 'than' value=INT
;

enum ConditionType:
    STRONGER='stronger' |
    WEAKER='weaker' |
    SMARTER='smarter' |
    DUMBER='dumber'
;

MakeDecision:
    'ask' question=STRING
    (case=DecisionCase)+
;

DecisionCase:
    answer=STRING '=>' stage=Stage
;

Fight:
    'fight with' (oponents+=[Monster] (',' oponents+=[Monster])*)?
;

Item:
    'item' name=ID '{'
    (bonuses+=Bonus)*
    (restrictions+=Restriction)?
    'value' (value=INT)
    '}';

Restriction:
    PositiveRest | NegativeRest;

PositiveRest:
    'can be used only by' (types+=[CharacterType] (',' types+=[CharacterType])*)
;

NegativeRest:
    'can\'t be used by' (types+=[CharacterType] (',' types+=[CharacterType])*)
;

Bonus:
    '+' value=INT 'to' type=('int' | 'str')
;

